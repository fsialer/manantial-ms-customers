plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.3'
	id 'io.spring.dependency-management' version '1.1.7'
	id "org.sonarqube" version "5.1.0.4882"
	id 'jacoco'
}

group = 'com.fernando.manantial-ms-customers'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

jacoco {
	toolVersion = "0.8.10"
}

sonar {
	properties {
		property("sonar.projectKey", System.getenv("SONAR_PROJECT_KEY"))
		property("sonar.organization", System.getenv("SONAR_ORGANIZATION"))
		property("sonar.host.url", System.getenv("SONAR_HOST_URL"))
		property("sonar.token", System.getenv("SONAR_TOKEN"))
		property("sonar.branch.name", System.getenv("SONAR_BRANCH_NAME"))
		property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
		property "sonar.exclusions", "**/config/**, **/ManantialMsCustomersApplication.*, **/validation/*.*, **/mappers/**, **/k8s/**"
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springdoc:springdoc-openapi-starter-webflux-ui:2.8.6'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation("io.micrometer:micrometer-registry-prometheus")
	implementation 'org.springframework.kafka:spring-kafka'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation 'org.springframework.kafka:spring-kafka-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.jacocoTestReport {
	reports {
		xml.required = true  // Necesario para SonarQube
		html.required = true
		csv.required = false
	}

	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect { fileTree(dir: it, exclude: [
				'**/config/**',
				'**/*Config.class',
				'**/*Configuration.class',
				"**/*MapperImpl.class",
				'**/ManantialMsCustomersApplication.*',
				"**/ManantialMsCustomersApplicationTests.*",
				"**/validation/*.*",
				"**/alerts/*",
				"**/alertsmanager/*.*",
				"**/prometheus/*.*"
		]) }))
	}
}

tasks.test {
	finalizedBy tasks.jacocoTestReport  // Asegura que el reporte se genere despu√©s de las pruebas
}
